# Content API
contentApi {
    # Path for rendering site properties (site will be in context)
    site = Neos.Fusion:DataStructure {
        meta = Neos.Fusion:DataStructure {
            nodeType = ${site.nodeType.name}
            nodeName = ${site.nodeName}
            title = ${q(site).property('title')}
        }
        content = Neos.Fusion:DataStructure
    }
    # Path for rendering document properties (node, documentNode and site will be in context)
    document = Neos.Fusion:Case {
        documentType {
            # Check if a prototype for a document node with .Api suffix is declared
            condition = Neos.Fusion:CanRender {
                type = ${documentNode.nodeType.name + '.Api'}
            }
            type = ${documentNode.nodeType.name + '.Api'}
        }

        default {
            condition = true
            renderer = Networkteam.Neos.ContentApi:DefaultDocument
        }
    }
}

prototype(Networkteam.Neos.ContentApi:DefaultDocument) < prototype(Neos.Fusion:DataStructure) {
    meta = Neos.Fusion:DataStructure {
        nodeType = ${node.nodeType.name}
        title = ${q(node).property('title')}
        uriPathSegment = ${q(node).property('uriPathSegment')}
    }
    content = Neos.Fusion:DataStructure
}
